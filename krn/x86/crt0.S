//##################################################################################################
//
//  Kernel low-level asm code.
//
//##################################################################################################

//--------------------------------------------------------------------------------------------------▫
// low-level entry point
// now CPU in protected 32-bit mode
//--------------------------------------------------------------------------------------------------▫
.section .bootstrap  // work at phys addresses
.global _start
_start:

	mov $bootstrap_stack, %esp
	jmp bootstrap

.space 0x400
bootstrap_stack:

//--------------------------------------------------------------------------------------------------▫
// two variables for time accounting
//--------------------------------------------------------------------------------------------------▫
.section .data
.global cur_kentry_start
.global cur_kexit_end
cur_kentry_start:  .long -1 // kernel entry timer value for current thread
cur_kexit_end:     .long -1 // kernel exit  timer value for current thread

//--------------------------------------------------------------------------------------------------
//  Interrupt vectors
//--------------------------------------------------------------------------------------------------
.section .ivt, "ax"

.macro create_gate_wrapper gateno
gate_wrapper_\gateno:
	.if \gateno >= 0x20
	  push $0       // put 0 instead of error code for exceptions (<0x20)
	.endif
	// store registers
	push %eax
	push %ecx
	push %edx
	push %ebx
	push %ebp
	push %esi
	push %edi
	// store func params
	push %esp
	push $\gateno
	cld             // C code following the sysV ABI requires DF to be clear on function entry
	call x86_entry_trap
	// leave func params
	add $8, %esp
	// restore registers
	pop %edi
	pop %esi
	pop %ebp
	pop %ebx
	pop %edx
	pop %ecx
	pop %eax
	// leave error code
	add $4, %esp
	iret
.endm

.macro create_gate_wrappers_16 n0 n1 n2 n3 n4 n5 n6 n7 n8 n9 n10 n11 n12 n13 n14 n15
	create_gate_wrapper \n0
	create_gate_wrapper \n1
	create_gate_wrapper \n2
	create_gate_wrapper \n3
	create_gate_wrapper \n4
	create_gate_wrapper \n5
	create_gate_wrapper \n6
	create_gate_wrapper \n7
	create_gate_wrapper \n8
	create_gate_wrapper \n9
	create_gate_wrapper \n10
	create_gate_wrapper \n11
	create_gate_wrapper \n12
	create_gate_wrapper \n13
	create_gate_wrapper \n14
	create_gate_wrapper \n15
.endm

create_gate_wrappers_16 0x00 0x01 0x02 0x03 0x04 0x05 0x06 0x07 0x08 0x09 0x0a 0x0b 0x0c 0x0d 0x0e 0x0f
create_gate_wrappers_16 0x10 0x11 0x12 0x13 0x14 0x15 0x16 0x17 0x18 0x19 0x1a 0x1b 0x1c 0x1d 0x1e 0x1f
create_gate_wrappers_16 0x20 0x21 0x22 0x23 0x24 0x25 0x26 0x27 0x28 0x29 0x2a 0x2b 0x2c 0x2d 0x2e 0x2f
create_gate_wrappers_16 0x30 0x31 0x32 0x33 0x34 0x35 0x36 0x37 0x38 0x39 0x3a 0x3b 0x3c 0x3d 0x3e 0x3f
create_gate_wrappers_16 0x40 0x41 0x42 0x43 0x44 0x45 0x46 0x47 0x48 0x49 0x4a 0x4b 0x4c 0x4d 0x4e 0x4f
create_gate_wrappers_16 0x50 0x51 0x52 0x53 0x54 0x55 0x56 0x57 0x58 0x59 0x5a 0x5b 0x5c 0x5d 0x5e 0x5f
create_gate_wrappers_16 0x60 0x61 0x62 0x63 0x64 0x65 0x66 0x67 0x68 0x69 0x6a 0x6b 0x6c 0x6d 0x6e 0x6f
create_gate_wrappers_16 0x70 0x71 0x72 0x73 0x74 0x75 0x76 0x77 0x78 0x79 0x7a 0x7b 0x7c 0x7d 0x7e 0x7f
create_gate_wrappers_16 0x80 0x81 0x82 0x83 0x84 0x85 0x86 0x87 0x88 0x89 0x8a 0x8b 0x8c 0x8d 0x8e 0x8f
create_gate_wrappers_16 0x90 0x91 0x92 0x93 0x94 0x95 0x96 0x97 0x98 0x99 0x9a 0x9b 0x9c 0x9d 0x9e 0x9f
create_gate_wrappers_16 0xa0 0xa1 0xa2 0xa3 0xa4 0xa5 0xa6 0xa7 0xa8 0xa9 0xaa 0xab 0xac 0xad 0xae 0xaf
create_gate_wrappers_16 0xb0 0xb1 0xb2 0xb3 0xb4 0xb5 0xb6 0xb7 0xb8 0xb9 0xba 0xbb 0xbc 0xbd 0xbe 0xbf
create_gate_wrappers_16 0xc0 0xc1 0xc2 0xc3 0xc4 0xc5 0xc6 0xc7 0xc8 0xc9 0xca 0xcb 0xcc 0xcd 0xce 0xcf
create_gate_wrappers_16 0xd0 0xd1 0xd2 0xd3 0xd4 0xd5 0xd6 0xd7 0xd8 0xd9 0xda 0xdb 0xdc 0xdd 0xde 0xdf
create_gate_wrappers_16 0xe0 0xe1 0xe2 0xe3 0xe4 0xe5 0xe6 0xe7 0xe8 0xe9 0xea 0xeb 0xec 0xed 0xee 0xef
create_gate_wrappers_16 0xf0 0xf1 0xf2 0xf3 0xf4 0xf5 0xf6 0xf7 0xf8 0xf9 0xfa 0xfb 0xfc 0xfd 0xfe 0xff

.macro create_gate_table_records_16 n0 n1 n2 n3 n4 n5 n6 n7 n8 n9 n10 n11 n12 n13 n14 n15
	.long gate_wrapper_\n0
	.long gate_wrapper_\n1
	.long gate_wrapper_\n2
	.long gate_wrapper_\n3
	.long gate_wrapper_\n4
	.long gate_wrapper_\n5
	.long gate_wrapper_\n6
	.long gate_wrapper_\n7
	.long gate_wrapper_\n8
	.long gate_wrapper_\n9
	.long gate_wrapper_\n10
	.long gate_wrapper_\n11
	.long gate_wrapper_\n12
	.long gate_wrapper_\n13
	.long gate_wrapper_\n14
	.long gate_wrapper_\n15
.endm

.global gate_table
gate_table:
	create_gate_table_records_16 0x00 0x01 0x02 0x03 0x04 0x05 0x06 0x07 0x08 0x09 0x0a 0x0b 0x0c 0x0d 0x0e 0x0f
	create_gate_table_records_16 0x10 0x11 0x12 0x13 0x14 0x15 0x16 0x17 0x18 0x19 0x1a 0x1b 0x1c 0x1d 0x1e 0x1f
	create_gate_table_records_16 0x20 0x21 0x22 0x23 0x24 0x25 0x26 0x27 0x28 0x29 0x2a 0x2b 0x2c 0x2d 0x2e 0x2f
	create_gate_table_records_16 0x30 0x31 0x32 0x33 0x34 0x35 0x36 0x37 0x38 0x39 0x3a 0x3b 0x3c 0x3d 0x3e 0x3f
	create_gate_table_records_16 0x40 0x41 0x42 0x43 0x44 0x45 0x46 0x47 0x48 0x49 0x4a 0x4b 0x4c 0x4d 0x4e 0x4f
	create_gate_table_records_16 0x50 0x51 0x52 0x53 0x54 0x55 0x56 0x57 0x58 0x59 0x5a 0x5b 0x5c 0x5d 0x5e 0x5f
	create_gate_table_records_16 0x60 0x61 0x62 0x63 0x64 0x65 0x66 0x67 0x68 0x69 0x6a 0x6b 0x6c 0x6d 0x6e 0x6f
	create_gate_table_records_16 0x70 0x71 0x72 0x73 0x74 0x75 0x76 0x77 0x78 0x79 0x7a 0x7b 0x7c 0x7d 0x7e 0x7f
	create_gate_table_records_16 0x80 0x81 0x82 0x83 0x84 0x85 0x86 0x87 0x88 0x89 0x8a 0x8b 0x8c 0x8d 0x8e 0x8f
	create_gate_table_records_16 0x90 0x91 0x92 0x93 0x94 0x95 0x96 0x97 0x98 0x99 0x9a 0x9b 0x9c 0x9d 0x9e 0x9f
	create_gate_table_records_16 0xa0 0xa1 0xa2 0xa3 0xa4 0xa5 0xa6 0xa7 0xa8 0xa9 0xaa 0xab 0xac 0xad 0xae 0xaf
	create_gate_table_records_16 0xb0 0xb1 0xb2 0xb3 0xb4 0xb5 0xb6 0xb7 0xb8 0xb9 0xba 0xbb 0xbc 0xbd 0xbe 0xbf
	create_gate_table_records_16 0xc0 0xc1 0xc2 0xc3 0xc4 0xc5 0xc6 0xc7 0xc8 0xc9 0xca 0xcb 0xcc 0xcd 0xce 0xcf
	create_gate_table_records_16 0xd0 0xd1 0xd2 0xd3 0xd4 0xd5 0xd6 0xd7 0xd8 0xd9 0xda 0xdb 0xdc 0xdd 0xde 0xdf
	create_gate_table_records_16 0xe0 0xe1 0xe2 0xe3 0xe4 0xe5 0xe6 0xe7 0xe8 0xe9 0xea 0xeb 0xec 0xed 0xee 0xef
	create_gate_table_records_16 0xf0 0xf1 0xf2 0xf3 0xf4 0xf5 0xf6 0xf7 0xf8 0xf9 0xfa 0xfb 0xfc 0xfd 0xfe 0xff

